@
@  Copyright (c) 2014-2016, Texas Instruments Incorporated
@  All rights reserved.
@
@  Redistribution and use in source and binary forms, with or without
@  modification, are permitted provided that the following conditions
@  are met:
@
@  *  Redistributions of source code must retain the above copyright
@     notice, this list of conditions and the following disclaimer.
@
@  *  Redistributions in binary form must reproduce the above copyright
@     notice, this list of conditions and the following disclaimer in the
@     documentation and/or other materials provided with the distribution.
@
@  *  Neither the name of Texas Instruments Incorporated nor the names of
@     its contributors may be used to endorse or promote products derived
@     from this software without specific prior written permission.
@
@  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
@  AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
@  THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
@  PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
@  CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
@  EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
@  PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
@  OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
@  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
@  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE,
@  EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
@
@ ======== Hwi_asm_gnu.sv7A ========
@

        .global ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQ__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQC__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_excHandlerAsm__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_excHandlerDataAsm__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_excHandler__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_init__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_initVbar__I
        .global ti_sysbios_family_arm_a8_intcps_Hwi_vectors
        .global ti_sysbios_family_arm_a8_intcps_Hwi_intc
        .global ti_sysbios_family_xxx_Hwi_switchToTaskStack
        .global ti_sysbios_family_xxx_Hwi_switchToIsrStack

        .arm
        .align 2

@
@  ======== Hwi_dispatchIRQ ========
@
@  Here on IRQ interrupt
@
@  task stack while in C code (With ASID tagging):
@
@       sp + 0x30 =  cpsr_sys
@       sp + 0x2c =  RA
@       sp + 0x28 =  lr_sys
@       sp + 0x24 =  saved r12
@       sp + 0x20 =  saved r7
@       sp + 0x1c =  saved r6
@       sp + 0x18 =  saved r5
@       sp + 0x14 =  saved r4
@       sp + 0x10 =  saved r3
@       sp + 0x0c =  saved r2
@       sp + 0x08 =  saved r1
@       sp + 0x04 =  saved r0
@       sp + 0x00 =  tskKey
@
@  task stack while in C code (Without ASID tagging):
@
@       sp + 0x28 =  cpsr_sys
@       sp + 0x24 =  RA
@       sp + 0x20 =  lr_sys
@       sp + 0x1c =  saved r12
@       sp + 0x18 =  saved r5
@       sp + 0x14 =  saved r4
@       sp + 0x10 =  saved r3
@       sp + 0x0c =  saved r2
@       sp + 0x08 =  saved r1
@       sp + 0x04 =  saved r0
@       sp + 0x00 =  tskKey
@

        .section .text.ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQ__I
        .func ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQ__I

ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQ__I:
        sub     lr, lr, #4
        srsdb   sp!, #0x1f      @ push sys CPSR and LR onto SYS stack
                                @ and back up below CPSR and LR
        cps     #0x1f           @ switch back to System mode
.if (ti_sysbios_family_arm_a8_intcps_Hwi_enableAsidTagging__D == TRUE)
        push    {r0-r7, r12, lr}@ save required C registers
        ldr     r5, [sp, #40]   @ retrieve IRP
.else
        push    {r0-r5, r12, lr}@ save required C registers
        ldr     r5, [sp, #32]   @ retrieve IRP
.endif

#if (defined(__VFP_FP__) && !defined(__SOFTFP__))
        vstmdb  r13!, {D0-D7}   @ save vfp scratch regs
        vstmdb  r13!, {D16-D31} @ save vfp scratch regs
        fmrx    r1, FPEXC
        fmrx    r2, FPSCR
        push    {r1-r2}
#endif

.if (ti_sysbios_family_arm_a8_intcps_Hwi_enableAsidTagging__D == TRUE)
        movw    r1, #:lower16:ti_sysbios_family_arm_a8_Mmu_Module__state__V
        movt    r1, #:upper16:ti_sysbios_family_arm_a8_Mmu_Module__state__V
        ldr     r1, [r1]                 @ get page table address
        mrc     p15, #0, r6, c13, c0, #1 @ save old ASID in r6
        mrc     p15, #0, r7, c2, c0, #0  @ save MMU table address in r7
        dmb
        mov     r2, #0xFF
        mcr     p15, #0, r2, c13, c0, #1 @ change ASID to 255
        isb
        mcr     p15, #0, r1, c2, c0, #0  @ write new table address to TTBR
        isb
        mov     r2, #0
        mcr     p15, #0, r2, c13, c0, #1 @ change ASID to 0 (Full Access)
        isb
.endif

        mov     r4, sp          @ save sp
        bic     sp, sp, #0x7    @ align task stack to 8 bytes
        movw    r1, #:lower16:ti_sysbios_family_arm_a8_intcps_Hwi_taskDisable__C
        movt    r1, #:upper16:ti_sysbios_family_arm_a8_intcps_Hwi_taskDisable__C
        ldr     r1, [r1]
        cmp     r1, #0
        blxne   r1              @ call Task_disable()
        push    {r0}            @ save tskKey

        @ switch to isr stack
        movw    r1, #:lower16:ti_sysbios_family_xxx_Hwi_switchToIsrStack
        movt    r1, #:upper16:ti_sysbios_family_xxx_Hwi_switchToIsrStack
        blx     r1
        push    {r0}            @ save oldTaskSP

        mov     r0, r5          @ pass IRP as argument to dispatchIRQC
        mov     r5, sp          @ save sp
        bic     sp, sp, #0x7    @ align isr stack to 8 bytes

        movw    r1, #:lower16:ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQC__I
        movt    r1, #:upper16:ti_sysbios_family_arm_a8_intcps_Hwi_dispatchIRQC__I
        blx     r1

        mov     sp, r5          @ restore sp

        @ switch back to task stack if at the bottom of Hwi stack
        pop     {r0}            @ restore oldTaskSP
        movw    r1, #:lower16:ti_sysbios_family_xxx_Hwi_switchToTaskStack
        movt    r1, #:upper16:ti_sysbios_family_xxx_Hwi_switchToTaskStack
        blx     r1
        pop     {r0}            @ restore tskKey

        @ run task scheduler
        movw    r1, #:lower16:ti_sysbios_family_arm_a8_intcps_Hwi_taskRestoreHwi__C
        movt    r1, #:upper16:ti_sysbios_family_arm_a8_intcps_Hwi_taskRestoreHwi__C
        ldr     r1, [r1]
        cmp     r1, #0
        blxne   r1              @ call Task_restoreHwi()
                                @ returns with interrupts disabled
        mov     sp, r4          @ restore sp

.if (ti_sysbios_family_arm_a8_intcps_Hwi_enableAsidTagging__D == TRUE)
        dmb
        mov     r2, #0xFF
        mcr     p15, #0, r2, c13, c0, #1 @ change ASID to 255
        isb
        mcr     p15, #0, r7, c2, c0, #0  @ restore mmu table address
        isb
        mcr     p15, #0, r6, c13, c0, #1 @ restore ASID
        isb
.endif

#if (defined(__VFP_FP__) && !defined(__SOFTFP__))
        pop     {r1-r2}
        fmxr    FPEXC, r1
        fmxr    FPSCR, r2
        vldmia  r13!, {D16-D31} @ restore vfp scratch regs
        vldmia  r13!, {D0-D7}   @ restore vfp scratch regs
#endif

.if (ti_sysbios_family_arm_a8_intcps_Hwi_enableAsidTagging__D == TRUE)
        pop     {r0-r7, r12, lr}
.else
        pop     {r0-r5, r12, lr}
.endif
        rfeia   sp!
        .endfunc

@
@  ======== Hwi_init ========
@  Low level ARM mode-specific register initialization
@

        .section .text.ti_sysbios_family_arm_a8_intcps_Hwi_init__I
        .func ti_sysbios_family_arm_a8_intcps_Hwi_init__I

ti_sysbios_family_arm_a8_intcps_Hwi_init__I:

@ Initialize FIQ sp
        mrs     r12, cpsr
        bic     r12, r12, #0x1f @ switch to FIQ mode
        orr     r12, r12, #0x11 @ to initialize FIQ sp
        msr     cpsr_cf, r12    @

        mov     sp, r0          @ sp = initial fiq stack

@ Done
        mrs     r12, cpsr
        orr     r12, r12, #0x1f @ switch back to SYS mode
        msr     cpsr_cf, r12    @

        bx      lr
        .endfunc

@
@  ======== Hwi_initVbar ========
@  Set vector table base address
@

        .section .text.ti_sysbios_family_arm_a8_intcps_Hwi_initVbar__I
        .func ti_sysbios_family_arm_a8_intcps_Hwi_initVbar__I

ti_sysbios_family_arm_a8_intcps_Hwi_initVbar__I:

@ Set vector table base address
        movw    r0, #:lower16:ti_sysbios_family_arm_a8_intcps_Hwi_vectors
        movt    r0, #:upper16:ti_sysbios_family_arm_a8_intcps_Hwi_vectors
        mcr     p15, #0, r0, c12, c0, #0

        bx      lr
        .endfunc

        .end
